# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo

typeset -gA key

key[Esc]='\e'
key[Ctrl]='^'
key[Space]=' '
key[Alt]=${key[Esc]}
key[Enter]=${terminfo[cr]}
key[Tab]=${terminfo[ht]}
key[Shift-Tab]=${terminfo[cbt]}
key[F1]=${terminfo[kf1]}
key[F2]=${terminfo[kf2]}
key[F3]=${terminfo[kf3]}
key[F4]=${terminfo[kf4]}
key[F5]=${terminfo[kf5]}
key[F6]=${terminfo[kf6]}
key[F7]=${terminfo[kf7]}
key[F8]=${terminfo[kf8]}
key[F9]=${terminfo[kf9]}
key[F10]=${terminfo[kf10]}
key[F11]=${terminfo[kf11]}
key[F12]=${terminfo[kf12]}
key[Backspace]=${terminfo[kbs]}
key[Insert]=${terminfo[kich1]}
key[Home]=${terminfo[khome]}
key[PageUp]=${terminfo[kpp]}
key[Delete]=${terminfo[kdch1]}
key[End]=${terminfo[kend]}
key[PageDown]=${terminfo[knp]}
key[Up]=${terminfo[kcuu1]}
key[Left]=${terminfo[kcub1]}
key[Down]=${terminfo[kcud1]}
key[Right]=${terminfo[kcuf1]}
# key[Shift-Up]=${terminfo[kUP]}
# key[Shift-Left]=${terminfo[kLFT]}
# key[Shift-Down]=${terminfo[kDN]}
# key[Shift-Right]=${terminfo[kRIT]}
# key[Shift-PgUp]=${terminfo[kPRV]}
# key[Shift-PgDown]=${terminfo[kNXT]}
# key[Shift-Ins]=${terminfo[kIC]}
# key[Shift-Del]=${terminfo[kDC]}
# key[Shift-Home]=${terminfo[kHOM]}
# key[Shift-End]=${terminfo[kEND]}


typeset -gA key_modifiers

key_modifiers[Shift]=2
key_modifiers[Alt]=3
key_modifiers[Shift-Alt]=4
key_modifiers[Ctrl]=5
key_modifiers[Ctrl-Shift]=6
key_modifiers[Ctrl-Alt]=7
key_modifiers[Ctrl-Alt-Shift]=8
key_modifiers[Meta]=9
key_modifiers[Meta-Shift]=10
key_modifiers[Meta-Alt]=11
key_modifiers[Meta-Alt-Shift]=12
key_modifiers[Meta-Ctrl]=13
key_modifiers[Meta-Ctrl-Shift]=14
key_modifiers[Meta-Ctrl-Alt]=15
key_modifiers[Meta-Ctrl-Alt-Shift]=16

# being quiet (commented out sections of below for debug)
function bindkey {
	builtin bindkey "$@" 2>/dev/null
}

# emacs key bindings
bindkey -e

for mod_name mod_value in ${(kv)key_modifiers}; do
	key[$mod_name-Up]="\e[1;${mod_value}A"
	key[$mod_name-Left]="\e[1;${mod_value}D"
	key[$mod_name-Down]="\e[1;${mod_value}B"
	key[$mod_name-Right]="\e[1;${mod_value}C"
	key[$mod_name-PgUp]="\e[6;${mod_value}~"
	key[$mod_name-PgDown]="\e[5;${mod_value}~"
	key[$mod_name-Ins]="\e[2;${mod_value}~"
	key[$mod_name-Del]="\e[3;${mod_value}~"
	key[$mod_name-Home]="\e[1;${mod_value}H"
	key[$mod_name-End]="\e[1;${mod_value}F"

	# don't spam in a terminal when pressing keys with modifiers
	bindkey -s "${key[$mod_name-Right]}" ''
	bindkey -s "${key[$mod_name-Left]}" ''
	bindkey -s "${key[$mod_name-Up]}" ''
	bindkey -s "${key[$mod_name-Down]}" ''
	bindkey -s "${key[$mod_name-PgUp]}" ''
	bindkey -s "${key[$mod_name-PgDown]}" ''
	bindkey -s "${key[$mod_name-Ins]}" ''
	bindkey -s "${key[$mod_name-Del]}" ''
	bindkey -s "${key[$mod_name-Home]}" ''
	bindkey -s "${key[$mod_name-End]}" ''
done
unset mod_name mod_value key_modifiers

bindkey "${key[Home]}"   beginning-of-line
bindkey "${key[End]}"    end-of-line
bindkey "${key[Insert]}" overwrite-mode
bindkey "${key[Delete]}" delete-char
bindkey "${key[Up]}"     up-line-or-history
bindkey "${key[Down]}"   down-line-or-history
bindkey "${key[Left]}"   backward-char
bindkey "${key[Right]}"  forward-char

# Ctrl + Right/Left
bindkey "${key[Ctrl-Right]}" forward-word
bindkey "${key[Ctrl-Left]}" backward-word

# F1: Get the man 1 page for the command on the line.
bindkey "${key[F1]}"     run-help

# M-e for expanding path of typed command
bindkey "${key[Alt]}e"   expand-cmd-path

# these allow you to start typing a command and
# use the up/down arrow to auto complete from
# commands in your history
# bindkey "${key[Up]}"     up-line-or-search
# bindkey "${key[Down]}"   down-line-or-search
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "${key[Alt]}p"   history-beginning-search-backward-end
bindkey "${key[Alt]}n"   history-beginning-search-forward-end

# enable search by globs
# bindkey "${key[Ctrl]}R" history-incremental-pattern-search-backward
# bindkey "${key[Ctrl]}S" history-incremental-pattern-search-forward

# use Page-Up/Down to help navigate history.
bindkey "${key[PageUp]}"   beginning-of-history
bindkey "${key[PageDown]}" end-of-history

bindkey "${key[Ctrl]}x${key[Ctrl]}e" edit-command-line
bindkey "${key[Space]}" magic-space
bindkey "${key[Alt]}#" pound-insert

# load completion listing extensions
zmodload -i zsh/complist

# menu selection (needed zsh/complist module)
bindkey -M menuselect "${key[Shift-Tab]}"  reverse-menu-complete
bindkey -M menuselect "${key[Esc]}"   send-break
bindkey -M menuselect "${key[Ctrl]}w" undo
bindkey -M menuselect "${key[Enter]}" .accept-line
bindkey -M menuselect "${key[Space]}" accept-line
bindkey -M menuselect "${key[Esc]}${key[Enter]}" accept-and-hold
bindkey -M menuselect '/' accept-and-infer-next-history

# vim:ft=zsh
