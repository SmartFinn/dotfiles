# Modify the colors and symbols in these variables as desired
VCS_INFO_AHEAD="%F{blue}↑NUM%f"
VCS_INFO_BEHIND="%F{yellow}↓NUM%f"
VCS_INFO_STAGED="%F{yellow}±%f"
VCS_INFO_STASHED="%F{magenta}⍟NUM%f"
VCS_INFO_UNSTAGED="%F{green}±%f"
VCS_INFO_UNTRACKED="%F{cyan}⁇%f"

# VCS integration for command prompt using vcs_info
autoload -Uz vcs_info
# zstyle ':vcs_info:*+*:*' debug true
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' stagedstr     "${VCS_INFO_STAGED:-*}"
zstyle ':vcs_info:*' unstagedstr   "${VCS_INFO_UNSTAGED:-+}"
zstyle ':vcs_info:*' formats       '%b' '%c%u%m'
zstyle ':vcs_info:*' actionformats '%b' '%c%u%m %B%s-%a%%b'
zstyle ':vcs_info:git*+set-message:*' hooks git-aheadbehind git-stash git-untracked

### git: Show +N/-N when your local branch is ahead-of or behind remote HEAD.
# Make sure you have added misc to your 'formats': %m
function +vi-git-aheadbehind() {
	[ "$1" -eq 0 ] || return 0

	local count
	integer ahead behind

	count=$(command git rev-list --left-right --count HEAD...@{upstream} 2>/dev/null)
	count=( ${(ps:\t:)count} )

	ahead=${count[1]}
	(( ahead )) && hook_com[misc]+=${${VCS_INFO_AHEAD:=+NUM}//NUM/$ahead}

	behind=${count[-1]}
	(( behind )) && hook_com[misc]+=${${VCS_INFO_BEHIND:=-NUM}//NUM/$behind}

	return 0
}

### git: Show count of stashed changes (@N)
# Make sure you have added misc to your 'formats': %m
function +vi-git-stash() {
	[ "$1" -eq 0 ] || return 0

	integer stashes

	stashes=$(git stash list 2>/dev/null | wc -l)
	(( stashes )) && hook_com[misc]+=${${VCS_INFO_STASHED:=@NUM}//NUM/$stashes}

	return 0
}

### git: Show marker (...) if there are untracked files in repository
# Make sure you have added staged to your 'formats': %m
function +vi-git-untracked() {
	[ "$1" -eq 0 ] || return 0

	if [ -n "$(command git ls-files --exclude-standard --others)" ]; then
		# This will show the marker if there are any untracked files in repo
		hook_com[misc]+=${VCS_INFO_UNTRACKED:=...}
	fi

	return 0
}

autoload -Uz add-zsh-hook
add-zsh-hook chpwd vcs_info
add-zsh-hook precmd vcs_info

# vim:ft=zsh
