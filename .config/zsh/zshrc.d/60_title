# Change title to command name
function _set_title {
	[[ -n "$RANGER_LEVEL" ]] && return 0
	[[ -n "$LF_LEVEL" ]] && return 0
	set -- "${(@s/ /)1}"  # converts first arg to an array
	(( ${+builtins[$1]} )) && return 0
	# (( ${+aliases[$1]} )) && return 0
	case "$1" in
		sudo|ssh|sudoedit)
			set -- "${(@)@:#-*}"  # removes options (-*)
			printf "\e]2;%s\a" "$1:t $2:t"
			;;
		*)
			printf "\e]2;%s\a" "$1:t"
	esac
}

precmd_functions+=(_set_title)
preexec_functions+=(_set_title)

# Show bell for inactive tabs if command execution exceeds 5 sec
function _start_timer {
	_timer="$SECONDS"
}

function _show_bell {
	if [ "$((SECONDS - _timer))" -gt 5 ]; then
		print -n "\a"
	fi
}

preexec_functions+=(_start_timer)
precmd_functions+=(_show_bell)

# vim:ft=zsh
