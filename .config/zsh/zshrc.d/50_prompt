# Collapsed path from /etc/nginx/conf.d to /e/n/conf.d
function _prompt_collapsed_cwd() {
	local len_percent="${1:-30}"

	local -a cwd=( "${(s:/:)${(%):-%~}}" )
	integer len=$((COLUMNS * len_percent / 100))
	integer i

	for (( i=2; i < ${#cwd}; i++ )); do
		[[ "${(c)#cwd}" -gt "$len" ]] || break

		# if directory name begins from '.', '_' or '@'
		if [[ ${cwd[$i]:0:1} == (.|_|@) ]]; then
			# trim to 2 chars
			cwd[$i]="${cwd[$i]:0:2}"
		else
			# otherwise trim to 1 char
			cwd[$i]="${cwd[$i]:0:1}"
		fi
	done

	print "${(j:/:)cwd}"
}

function _prompt_collapsed_cwd_color() {
	print "%F{244}$(_prompt_collapsed_cwd)%f"
}

function _prompt_info_line() {
	print "%F{244}%n%f@%F{244}%m%f"
}

function _prompt_ranger() {
	if [ -n "$RANGER_LEVEL" ]; then
		print "%F{32}(ranger)%f"
	fi
}

function _prompt_git_indicators() {
	if [ -n "${vcs_info_msg_1_}" ]; then
		print -n "${vcs_info_msg_1_}"
	fi
}

function __prompt() {
	print -n "$(_prompt_ranger)"
	print -n "%(?.%F{green}.%F{red})%(!.#.$)%f %(2L.>>.>) "
}

function __prompt_right() {
	if [ -n "$SSH_CONNECTION" ]; then
		print -n "%B%F{green}%n@%m%f%b:"
	fi

	print -n "$(_prompt_collapsed_cwd_color)"

	if [ -n "${vcs_info_msg_0_}" ]; then
		print -n "%F{248}:${vcs_info_msg_0_}%f${vcs_info_msg_1_}"
	fi
}

PROMPT='$(__prompt)'
RPROMPT='$(__prompt_right)'
SPROMPT='  %B%F{red}%R%f%b > %B%F{green}%r%f%b? [y/N/a/e]: '
PROMPT2='(%_)> '
PROMPT3='? > '
PROMPT4='+%N:%i> '

# vim:ft=zsh
